#!/bin/sh
# Start it with sh... \
  exec tclsh8.4 "$0" "$@"

#
# Start by initializing some variables.
#
set DATE {date}
set FIND {find}
set UNAME {uname}
set GREP {grep}
set SORT {sort}
set UNIQ {uniq}
set CAT {cat}
set MV {mv}
set LS {ls}
set dt [eval exec $DATE +%Y%m%d]

set FIDO_ROOT {/etc/trpwr/fido}
set FIDO_TMP "/tmp/tfido_tmp.$dt"
set FIDO_TMP2 "/tmp/tfido_tmp.$dt"
set FIDO_LIS "fido.list"
set FIDO_EXC "fido.excl"
# This must be set unless you are running it on the same system, which
# is *NOT* recommended.
set default_port {2039}
set default_server {127.0.0.1}

#
# The following is the password with which the line is encrypted using the blowfish
# algorythm.
#
set password "Sample fido password...change this."
set fido_size 512

#
# Load the shared object.
#
set SLE [info sharedlibextension]
if [ catch { load ./libcdbtcl$SLE } result ] {
  puts "Could not load ./libcdbtcl$SLE: $result"
  exit 1
}

#
# Check the sha1 signature of this file.
if [ catch { source ./tfido.sha1.tcl } result ] {
  puts "Could not source tfido.sha1.tcl: $result"
  exit 1
}
set this_fidoSha1 [ sha1sum tfido ]
if { $fidoSha1 != $this_fidoSha1 } {
  puts "WARNING: sha1 signature for tfido doesn't match!"
  exit 1
}

set this_cdbtclSha1 [ sha1sum libcdbtcl.so ]
if { $cdbtclSha1 != $this_cdbtclSha1 } {
  puts "WARNING: sha1 signagure for libcdbtcl.so doesn't match!"
  exit 1
}

#
# Traverse through a list of directories, creating a file list from the
# directories.  This file list will be used as a basis for integrity checks
# of individual files.
#

#
# fileModeType (mode, type)
#
# Returns a string of the type -rwxrwxrwx (similar to that returned by ls -l)
# for the file whose mode is mode (given by stat(mode)) and whose type is type
# given by stat(type).

proc fileModeType { mode type } {
  #
  # OK, we are going to look at the mode value and do some bit-flipping
  # to determine what type of file/directory this is.  What follows are
  # masks (in hex) and what value they return when applied against the
  # mode.
  # 
  #                                    User    Grp     Other
  #             ..._ _ _ _ _ _ _ _ _ | _ _ _ | _ _ _ | _ _ _
  #
  # 0xfffffe &                       |                      |
  # 0x0001                                                | |
  # 0x0002                                              | |
  # 0x0004                                            | |
  # 0x0008                                        | |
  # 0x0010                                      | |
  # 0x0020                                    | |
  # 0x0040                                | |
  # 0x0080                              | |
  # 0x0100                            | |
  # 0x800   &   Gives whether SUID
  # 0x400   &   Gives whether GUID
  # 0x200   &   Gives whether Sticky bit on
  #
  # Then, I can use 'file type' and 'file attributes' to get the rest of
  # the info I need.
  # 

  #
  # Get the "other" information
  #
  set mask [ expr $mode & 0x0001 ]
  if { $mask == 1 } {
    set o_x x
  } else {
    set o_x -
  }
  set mask [ expr $mode & 0x0002 ]
  if { $mask == 2 } {
    set o_w w
  } else {
    set o_w -
  }
  set mask [ expr $mode & 0x0004 ]
  if { $mask == 4 } {
    set o_r r
  } else {
    set o_r -
  }
  #
  # check for sticky bit
  #
  set mask [ expr $mode & 0x0200 ]
  if { $mask == 512 } {
    if { $o_x == "x" } {
      set o_x t
    } else {
      set o_x T
    }
  }
  #
  # Group information
  #
  set mask [ expr $mode & 0x0008 ]
  if { $mask == 8 } {
    set g_x x
  } else {
    set g_x -
  }
  set mask [ expr $mode & 0x0010 ]
  if { $mask == 16 } {
    set g_w w
  } else {
    set g_w -
  }
  set mask [ expr $mode & 0x0020 ]
  if { $mask == 32 } {
    set g_r r
  } else {
    set g_r -
  }
  #
  # Check for guid
  #
  set mask [ expr $mode & 0x0400 ]
  if { $mask == 1024 } {
    if { $g_x == "x" } {
      set g_x s
    } else {
      set g_x S
    }
  }
  #
  # User information
  #
  set mask [ expr $mode & 0x0040 ]
  if { $mask == 64 } {
    set u_x x
  } else {
    set u_x -
  }
  set mask [ expr $mode & 0x0080 ]
  if { $mask == 128 } {
    set u_w w
  } else {
    set u_w -
  }
  set mask [ expr $mode & 0x0100 ]
  if { $mask == 256 } {
    set u_r r
  } else {
    set u_r -
  }
  #
  # Check for suid
  #
  set mask [ expr $mode & 0x0800 ]
  if { $mask == 2048 } {
    if { $u_x == "x" } {
      set u_x s
    } else {
      set u_x S
    }
  }
  #
  # See if it is a directory, regular file or some other kind of file
  #
  switch -exact -- $type {
    file { set f_d - }
    directory { set f_d d }
    characterSpecial { set f_d c }
    blockSpecial { set f_d b }
    fifo { set f_d f }
    link { set f_d l }
    socket { set f_d s }
    default { set f_d ? }
  }
  set mode_str [ format "%s%s%s%s%s%s%s%s%s%s" $f_d $u_r $u_w $u_x $g_r $g_w $g_x $o_r $o_w $o_x ]
  return $mode_str
}

#
# Set port and server based on command-line arguments:
#
# fido (without arguments) -> PORT=default, SERVER=default (set in config).
# fido one (one argument) -> PORT=one, SERVER=default (set in config).
# fido one two (two arguments) -> PORT=one, SERVER=two (nothing from config).
#
if { $argc == 0 } {
  # Defaults
  set PORT $default_port
  set SERVER $default_server
}

if { $argc == 1 } {
  # Get port from the command-line
  set PORT [ lindex $argv 0 ]
  set SERVER $default_server
}

if { $argc == 2 } {
  # Get both from the command-line
  set PORT [ lindex $argv 0 ]
  set SERVER [ lindex $argv 1 ]
}

set clock1 [ dbtime ]

#
# Open $FIDO_LIS for reading.
#
if [ catch { set file [ open $FIDO_LIS r ] } error ] {
  puts stderr "Could not open $FIDO_LIS: $error"
  return 1
}

#
# Get each line into an element of dirList to make for easy list processing.
#
if [ catch { set dirList [ split [ read $file ] "\n" ] } ] {
  puts stderr "Could not get the contents of $FIDO_LIS"
  return 1
}

catch { close $file }

#
# Remove the tmp file if it exists - ignore the results of this.
#
set isFile [file exists $FIDO_TMP]
if {$isFile} {
  file delete $FIDO_TMP
}

#
# Traverse the directories and generate a list of files to process.
#
foreach dir $dirList {
  if { $dir != "" } {
    if [ catch { eval exec $FIND $dir -xdev  >> $FIDO_TMP } \
	   error ] {
      puts stderr "Could not run find on $dir: $error"
    }
  }
}

#
# Handle excludes
#
if [ file exists $FIDO_EXC ] {
  if [ catch { set excludes [ open $FIDO_EXC r ] } error ] {
    puts stderr "Could not open $FIDO_EXC: $error"
    return 1
  }
  set exclude_list [ split [ read $excludes ] "\n" ]
  foreach item $exclude_list {
    if { $item != "" } {
      catch {eval exec $MV $FIDO_TMP $FIDO_TMP2}
      catch {eval exec $GREP -v $item $FIDO_TMP2 > $FIDO_TMP}
    }
  }
  file delete $FIDO_TMP2
}

if [ catch { set file [ open $FIDO_TMP r ] } error ] {
  puts stderr "Could not open $FIDO_TMP: $error"
  return 1
}

#
# OK, generate a list from the contents of the file.
#
set tmpFileList [ split [ read $file ] "\n" ]

#
# Create fileList as an empty list and add to it the files without pipes in
# the names.  For each one with a '|', print a warning that we are ignoring it.
#
set fileList [ list ]
foreach item $tmpFileList {
  set status [ string first "|" $item ]
  if { $status == "-1" } {
    # OK, add to the list
    lappend fileList $item
  } else {
    puts stdout "WARNING: file \"$item\" has a pipe (|) in the name; skipping"
    continue
  }
}

catch { close $file }
file delete $FIDO_TMP

set sock [ socket $SERVER $PORT ]

if [ catch { set hostnm [ eval exec $UNAME -n ] } error ] {
  puts stderr "Could not get hostname: $error"
  return 1
}

set num_list_items [ llength $fileList ]
puts stderr "Processing $num_list_items files"
puts stderr ""
puts stderr "Sending hostname $hostnm"
puts stderr ""

foreach item $fileList {
  if { $item != "" } {
    set print 0
    if [ catch { file stat $item fstat } result ] {
      puts stderr "Could not stat $item: $result"
      continue
    }
    #
    # We can't handle fifo, socket, char spl or blk spl.
    #
    if { $fstat(type) == "fifo" } {
      puts stderr "$item is a fifo...skipping."
      continue
    }
    if { $fstat(type) == "socket" } {
      puts stderr "$item is a socket...skipping."
      continue
    }
    if { $fstat(type) == "characterSpecial" } {
      puts stderr "$item is a character special device...skipping"
      continue
    }
    if { $fstat(type) == "blockSpecial" } {
      puts stderr "$item is a block special device...skipping"
      continue
    }
    #
    # end fifo, socket, character special, block special
    #
    if [ catch { set md5num [ md5sum "$item" ] } result ] {
      puts stderr "Could not md5sum $item: $result"
      continue
    } else {
      set print 1
    }
    if [ catch { set sha1num [ sha1sum "$item" ] } result ] {
      set print 0
      puts stderr "Could not sha1sum $item: $result"
      continue
    }
    if [ catch { set ripenum [ rmd160sum "$item" ] } result ] {
      set print 0
      puts stderr "Could not rmd160sum $item: $result"
      continue
    }

    #
    # Get the type from the 'file type' command - handles sym links
    # correctly.
    #
    catch {set type [file type $item]}
    set perms [ fileModeType $fstat(mode) $type ]
    set attrs [ file attributes $item ]
    set user [ lindex $attrs 3 ]
    set grp [ lindex $attrs 1 ]
    # 20021007 - DFM
    set ctime [ clock format $fstat(ctime) -format "%Y/%m/%d-%H:%M:%S" ]
    if { $print == 1 } {
      set outstr1 [ format "%s|%s|%s|%s|%d|%s|%s|%s|%s|%s" \
		      $hostnm $item $user $grp $fstat(size) \
		      $perms $ctime $md5num $sha1num $ripenum ]
      set outstr [ dbencrypt $outstr1 $fido_size $password ]
      puts $sock "$outstr"
    }
  }
}

set outstr [ dbencrypt "**END**" $fido_size $password ]
puts $sock $outstr

close $sock

set clock2 [ dbtime ]
set nowTime [ expr $clock2-$clock1 ]
set thisTime [ format "%6.4f" $nowTime ]

puts stderr ""
puts stderr "Finished tfido run in $thisTime seconds"
puts stderr ""

exit 0

